import meta::pure::functions::collection::*;
import meta::rel::metamodel::*;
import meta::rel::metamodel::render::constant::*;
import meta::rel::utils::*;


/////////////////////////
// Rendering constants //
/////////////////////////

function meta::rel::metamodel::render::constant::renderConstant(constant: RelString[1]): String[1] {
  format('"%s"', $constant.value)
}

function meta::rel::metamodel::render::constant::renderConstant(constant: RelInteger[1]): String[1] {
  $constant.value->toString()
}

function meta::rel::metamodel::render::constant::renderConstant(constant: RelFloat[1]): String[1] {
  $constant.value->toString()
}

function meta::rel::metamodel::render::constant::renderConstant(constant: RelDate[1]): String[1] {
  $constant.value->toString()
}

function meta::rel::metamodel::render::constant::renderConstant(constant: RelDateTime[1]): String[1] {
  format(
    '%d-%02d-%02dT%02d:%02d:%02dZ',
    [
      $constant.value->year(),
      $constant.value->monthNumber(),
      $constant.value->dayOfMonth()->toOne(),
      $constant.value->hour(),
      $constant.value->minute(),
      $constant.value->second()
    ]
  )
}

function meta::rel::metamodel::render::constant::renderConstant(dontCare: DontCare[1]): String[1] {
  '_'
}

function meta::rel::metamodel::render::constant::renderConstant(constant: Constant[1]): String[1] {
  $constant->match([
    c: RelString[1]   | $c->renderConstant(),
    c: RelInteger[1]  | $c->renderConstant(),
    c: RelFloat[1]    | $c->renderConstant(),
    c: RelDate[1]     | $c->renderConstant(),
    c: RelDateTime[1] | $c->renderConstant(),
    c: Label[1] |
      if ($c.identifier->isAlphaNumeric() && ($c.identifier->size() > 0) && !$c.identifier->substring(0, 1)->isDigit(),
        | format(':%s', $c.identifier),
        | format(':"%s"', $c.identifier)
        )
  ])
}

function meta::rel::metamodel::render::constant::renderConstant(constants: Constant[*]): String[1] {
  $constants->stringify(c | $c->renderConstant(), ', ')
}
